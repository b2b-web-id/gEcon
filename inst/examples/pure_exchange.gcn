# ############################################################################
# (c) Chancellery of the Prime Minister of the Republic of Poland 2012-2015  #
# (c) Grzegorz Klima, Karol Podemski, Kaja Retkiewicz-Wijtiwiak 2015-2018    #
# License terms can be found in the file 'LICENCE'                           #
#                                                                            #
# Authors: Grzegorz Klima, Karol Podemski, Kaja Retkiewicz-Wijtiwiak         #
# ############################################################################
# Pure exchange model with two consumers and three goods
# ############################################################################


options
{
    output logfile = TRUE;
    output LaTeX = TRUE;
};


indexsets
{
    goods = {'1' .. '3'};
    agents = {'A', 'B'};
};


block <a::agents> AGENTS
{
    controls
    {
        <g::goods> C<a,g>[];
    };
    objective
    {
        U<a>[] = PROD<g::goods>(C<a,g>[] ^ alpha<a,g>);
    };
    constraints
    {
        SUM<g::goods>(p<g>[] * C<a,g>[]) = SUM<g::goods>(p<g>[] * e<a,g>[]);
    };
    identities
    {
        <g::goods> e<a,g>[] = e_calibr<a,g>;
    };
};


block EQUILIBRIUM
{
    identities
    {
        # numeraire
        p<'1'>[] = 1;
        # goods market clearing
        <g::goods\'1'> SUM<a::agents>(C<a,g>[]) = SUM<a::agents>(e<a,g>[]);
    };
};
